#!/usr/bin/env perl6

sub MAIN($name, :$description) {
    my $dir = $name.subst(/'::'/, '-', :g);
    mkdir $dir;
    chdir $dir;

    mkdir 'lib';
    mkdir 't';

    my $desc = $description 
            // prompt("Please enter a short description: ");;
    my $user = qx/git config --global github.user/.chomp;
    unless $user.chars > 0 {
        $user = prompt("Please enter your github username: ");
    }
    my $gitrepo = "git://github.com/$user/$dir.git";
    given open('META.info', :w) {
        .say:   '{';
        .say: qq{    "name"        : "$name",};
        .say:   '    "version"     : "*",';
        .say: qq{    "description" : "$desc",};
        .say:   '    "depends"     : [],';
        .say: qq{    "source-url"  : "$gitrepo"};
        .say:    '}';
        .close;
    }
    run "git init";
    run "git remote add origin git@github.com:$user/$dir.git";

    my $token = qx/git config --global github.token/.chomp;
    if $token.chars > 0 {
        if prompt("Create a github repo? (requires curl) [y/n]: ") ~~ /^y/ {
            run "curl -s -F 'login=$user' -F 'token=$token' http://github.com/api/v2/yaml/repos/create -F 'name=$dir'";
        }
    }
    say "You're ready to hack. The git repository is already created for you, and configured to push to $user/$dir. Have fun!";
}

# vim: ft=perl6
